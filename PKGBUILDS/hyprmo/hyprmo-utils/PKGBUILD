# Maintainer: Aren <aren@peacevolution.org>
# Contributor: dni <office@dnilabs.com>

pkgname=('hyprmo-utils' 'hyprmo-utils-hyprland')
pkgver=0.01.0
pkgrel=1
pkgdesc="Utility scripts, programs, and configs that hold the hyprmo UI environment together"
url="https://github.com/Raezroth/sxmo-utils-hyprland"
arch=('x86_64' 'armv7h' 'aarch64')
license=('AGPL3')
makedepends=('libx11' 'xorgproto' 'linux-headers' 'busybox' 'scdoc')
install=hyprmo-utils.install

# Rebase to Hyprland

source=("$pkgname-$pkgver.tar.gz::https://github.com/Raezroth/sxmo-utils-hyprland/archive/refs/heads/master.tar.gz"
        '0001-avoid-conflicting-with-systemd-services.patch'
        '0002-Partially-revert-7a77851c.patch'
        '00-hyprmo.rules'
        '10-hyprmo.conf'
        'hyprmo-setpermissions.service')
sha512sums=('SKIP'
            '2a11f00fa5f508a0a5af3fb28730317de1cc99bb340ef724e5566aaa9bccec44563da3094028e8f447db63500bbedf46c25c1db250abf771f2e3549038e59224'
            'd327fcc2abdc25889f650d7c47a6b9cc67e85539954b45d4097ff9bffb75133244ea8b0d96339f9c3280a544395dcc9b96d59c6a9a117b7f685afe5123d377eb'
            'd705fc9611716a73c89da94bf3abc9292e9069bc994b0bd097159f4babd83d4693d5cc3e42336739ac46a8c6b4a0f39d077047202901dc5066df75e597450fa7'
            'b69a09f98f8a5cad8164cd48c5f6a78f2ce0e9818fb77ccf2a3e243cd0f2023e438022b20aad9e6b5edd90e1bb862326db79dc423630682d4c43e897090d3da1'
            '532c3045f52f872704876bdf56afbc7470c5d17e46655dd4040d545b6d3eb34a8791909e554c560471f9494c7382107839bcff1e48be242d5eea0092482165cd')

prepare() {
  cd "$pkgname-hyprland-master"

  patch -p1 < '../0001-avoid-conflicting-with-systemd-services.patch'
  patch -p1 < '../0002-Partially-revert-7a77851c.patch'
}

build() {
  cd "$pkgname-hyprland-master"

  make
}

package_hyprmo-utils() {
  backup=('etc/doas.conf')
  depends=(
    # Shell utilities
    'bc'
    'curl'
    'gawk'
    'gojq'
    'less'
    'net-tools' # netstat command
    'pnc'

    # Core dependcies
    'callaudiod'
    'opendoas'
    'geoclue'
    'iio-utils'
    'inotify-tools'
    'mmsd-tng'
    'mnc' # for suspend
    'modemmanager'
    'ncurses' # for the weather script
    'networkmanager'
    'superd'
    'vvmd'
    'xdg-user-dirs'

    # Audio
    # TODO: alsa-utils is only required for sxmo_record.sh, it can probably be
    # removed soon
    'alsa-utils'
    'bluez'
    'bluez-utils'
    'libpulse'
    'pulseaudio'
    'pulseaudio-alsa'

    # Core GUI dependencies
    'brightnessctl'
    'conky'
    'dunst'
    'libnotify'
    'lisgd'
    'mpv'
    'hyprmo-ui'
    'ttf-dejavu-nerd'
    'upower')
  optdepends=('bemenu-ncurses: sxmo menus over ssh'
              'bonsai: better multikey script'
              'clickclack: haptic feedback'
              'codemadness-frontends: Youtube & Reddit scripts'
              'j4-dmenu-desktop: all apps menu generated from .desktop files'
              'mediainfo: sound recorder'
              'pipewire-pulse: recommended sound server'
              'sfeed: Rss and atom feeds'
              'vim: The default editor'
              'yt-dlp: Play videos from the web')

  make -C "$pkgname-hyprland-master" \
    OPENRC=0 \
    DESTDIR="$pkgdir" \
    EXTERNAL_SERVICES=0 \
    install-scripts install-docs

  # Allow access to ModemManager and NetworkManager
  install -Dm644 -t "$pkgdir/usr/share/polkit-1/rules.d" "00-hyprmo.rules"

  # Prevent logind from handling the power button
  install -Dm644 -t "$pkgdir/etc/systemd/logind.conf.d" "10-hyprmo.conf"

  # Fixup mode to match polkit
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  # fix directory permissions, TODO this should be done upstream
  chmod 750 "$pkgdir/etc/doas.d"

  # set the sxmo version, releases have a chronic issue of not updating this
  echo "$pkgver" > "$pkgdir/usr/share/hyprmo/version"

  # HACK: doas is built without --with-doas-confdir so install the sxmo config directly
  install -Dm640 "$pkgdir/etc/doas.d/hyprmo.conf" "$pkgdir/etc/doas.conf"

  install -Dm644 "$srcdir/hyprmo-setpermissions.service" "$pkgdir/usr/lib/systemd/system/hyprmo-setpermissions.service"
}

package_hyprmo-utils-hyprland() {
  pkgdesc="Use Hyprland as the window manager with sxmo"
  url="https://null.null/null"
  arch=('any')
  provides=('hyprmo-ui')
  replaces=('sxmo-utils-hyprland')
  install=
  depends=('rofi'
           'foot'
           'grim'
           'slurp'
	   'hyprlang'
           'hyprland'
           'hyprpaper'
           'hyprlock'
	   'hyprcursor'
	   'xdg-desktop-portal-hyprland'
           'wl-clipboard'
           'wob'
           'wtype'
           'wvkbd'
           'xorg-xwayland')
  #optdepends=('')

  make -C "$pkgbase-hyprland-master" DESTDIR="$pkgdir" install-hyprland
}

