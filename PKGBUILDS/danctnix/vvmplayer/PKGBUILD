# Maintainer: Aren Moynihan <aren@peacevolution.org>
pkgname=vvmplayer
pkgver=2.6
pkgrel=1
pkgdesc="A frontend GUI to display, play, and delete visual voicemails"
url="https://gitlab.com/kop316/vvmplayer"
arch=('x86_64' 'armv7h' 'aarch64')
license=('GPL-3.0-or-later AND CC0-1.0')
depends=(
  'callaudiod'
  'evolution-data-server'
  'gst-plugins-base'
  'gst-plugins-good'
  'gst-plugins-ugly'
  'gtk4'
  'libadwaita'
  'vvmd'
)
makedepends=(meson)
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/kop316/vvmplayer/-/archive/$pkgver/vvmplayer-$pkgver.tar.gz")
sha256sums=('49869baa9d063b88b6c09596df65f794e403ea1f80cc49455535be07071b67a1')

build() {
  arch-meson "$pkgname-$pkgver" build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
